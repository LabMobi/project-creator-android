plugins {
    alias(libs.plugins.android.library)
    alias(libs.plugins.kotlin.android)
}

android {
    namespace = "mobi.lab.sample"
    compileOptions {
        sourceCompatibility = JavaVersion.valueOf(libs.versions.javaCompatibility.get())
        targetCompatibility = JavaVersion.valueOf(libs.versions.javaCompatibility.get())
    }

    kotlinOptions {
        jvmTarget = libs.versions.jvmTarget.get()
    }

    defaultConfig {
        compileSdk = libs.versions.android.sdk.compile.get().toInteger()
        targetSdkVersion(libs.versions.android.sdk.target.get())
        minSdkVersion(libs.versions.android.sdk.min.get())
        version = rootProject.ext.libraryVersion
    }

    libraryVariants.configureEach { variant ->
        variant.outputs.all { output ->

            if (variant.buildType.getName().contains("release")) {
                // Don"t add the variant name to release artefacts
                outputFileName = "${base.archivesName.get()}-${rootProject.ext.libraryVersion}.aar"
            } else {
                outputFileName = "${base.archivesName.get()}-${variant.name}-${rootProject.ext.libraryVersion}.aar"
            }
        }
    }

    lint {
        fatal("StopShip")
    }

    testOptions.unitTests.all {
        testLogging {
            events("passed", "skipped", "failed", "standardOut", "standardError")
            // Always show output even if the result has not changed
            outputs.upToDateWhen { false }
            // Show standard stream outputs if used
            showStandardStreams = true
        }
    }
}

dependencies {
    implementation libs.kotlin
    implementation libs.androidx.appcompat
    
    testImplementation libs.test.junit
    testImplementation libs.test.junit.kotlin
}

tasks.register('buildAll') {
    group = "Build"
    description = "Build the library and the sample application"
    dependsOn ":sample:build", "build"
}

build.mustRunAfter(clean)

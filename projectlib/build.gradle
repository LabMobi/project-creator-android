plugins {
    alias(libs.plugins.android.application) apply false
    alias(libs.plugins.android.library) apply false
    alias(libs.plugins.kotlin.android) apply false
    alias(libs.plugins.ktlint.gradle)
    alias(libs.plugins.gradleVersions)
    alias(libs.plugins.detekt)
}

ext {
    libraryVersion = "0.0.1"
}

subprojects {
    // KTLint (Kotlin code style enforcer)
    apply(plugin: libs.plugins.ktlint.gradle.get().pluginId)
    ktlint {
        outputToConsole = true
    }

    // Detekt (Kotlin static analyser)
    apply(plugin: libs.plugins.detekt.get().pluginId)
    detekt {
        // Include all rules
        allRules = true
        // preconfigure defaults (see the rule set file below for explanations)
        buildUponDefaultConfig = true
        // point to your custom config defining rules to run, overwriting default behavior
        config.setFrom("${project.rootDir}/config/detekt/detekt-config.yaml")
    }

    tasks.named("detekt").configure {
        jvmTarget = "1.8"
        reports {
            html.required.set(true)
            xml.required.set(false)
            txt.required.set(false)
        }
    }

    afterEvaluate {
        // Make sure detekt is executed with all the other linters for all modules
        check.dependsOn "detekt"
    }

    task checkCode(dependsOn: ["detekt", "ktlintCheck"])
    checkCode.group = "Verification"
    checkCode.description = "Run detekt and ktlint"
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

// Don't show non stable updates for current stable versions
def isNonStable = { String version ->
    def stableKeyword = ["RELEASE", "FINAL", "GA"].any { keyword -> version.toUpperCase().contains(keyword) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

dependencyUpdates {
    checkForGradleUpdate = true

    resolutionStrategy {
        componentSelection {
            all {
                if (isNonStable(candidate.version) && !isNonStable(currentVersion)) {
                    reject("Release candidate")
                }
            }
        }
    }
}

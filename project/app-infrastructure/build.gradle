plugins {
    alias(libs.plugins.android.library)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.kotlin.kapt)
    alias(libs.plugins.kotlin.ksp)
    alias(libs.plugins.hilt)
}

android {
    namespace = "mobi.lab.sample.infrastructure"

    buildFeatures {
        buildConfig = true
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    compileOptions {
        // Flag to enable support for the new language APIs
        coreLibraryDesugaringEnabled = true

        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    defaultConfig {
        compileSdkVersion(libs.versions.android.sdk.compile.get())
        targetSdkVersion(libs.versions.android.sdk.target.get())
        minSdkVersion(libs.versions.android.sdk.min.get())
    }

    buildTypes {
        release {
            minifyEnabled = true
            consumerProguardFiles("proguard-rules.pro")
        }
    }

    testOptions.unitTests.all {
        testLogging {
            events("passed", "skipped", "failed", "standardOut", "standardError")
            // Always show output even if the result has not changed
            outputs.upToDateWhen { false }
            // Show standard stream outputs if used
            showStandardStreams = true
        }
    }
}

hilt {
    // This flag reduces incremental compilation times by reducing how often an incremental change causes a rebuild of the Dagger components.
    // See https://dagger.dev/hilt/gradle-setup.html#aggregating-task
    enableAggregatingTask = true
}

kapt {
    // If Hilt is used in a Kotlin project, then Kapt should be configured to keep the correct error types.
    // See https://dagger.dev/hilt/gradle-setup.html#using-hilt-with-kotlin
    correctErrorTypes = true
}

// Set kapt jvm version. https://youtrack.jetbrains.com/issue/KT-55947/Unable-to-set-kapt-jvm-target-version
tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    libsHelper.addUnitTestDependencies(it)
    libsHelper.addDependencyInjectionDependencies(it)

    implementation libs.kotlin
    coreLibraryDesugaring libs.jdk.desugar

    implementation libs.timber
    implementation libs.rxjava
    implementation libs.androidx.core

    // Json
    implementation libs.moshi
    implementation libs.moshi.adapters
    implementation libs.kotshi.api
    ksp libs.kotshi.compiler

    // Retrofit
    implementation libs.retrofit
    implementation libs.retrofit.adapter.rxjava
    implementation libs.retrofit.converter.moshi
    implementation libs.okhttp.logging.interceptor

    // Glide
    implementation libs.glide
    implementation (libs.glide.okhttp3) {
        exclude group: "glide-parent"
    }
    ksp libs.glide.compiler

    implementation project(":app-domain")
    implementation project(":app-common")
}

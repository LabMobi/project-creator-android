/*
 Helper functions to add multiple dependencies to different configurations
 Version Catalogs is used for version management. See gradle/libs.versions.toml
 */

// This definition type lets us define all functions in a single place and access them via deps.
ext.libsHelper = [
        addUnitTestDependencies: { handler ->
            handler.testImplementation libs.test.junit
            handler.testImplementation libs.test.junit.kotlin
            handler.testImplementation libs.test.mockito.core
            // Mockito
            // https://javadoc.io/doc/org.mockito/mockito-core/latest/org.mockito/org/mockito/Mockito.html#0.3
            // https://github.com/gradle/gradle/issues/2298
            // Also see root build.gradle for additional configuration
            handler.mockitoAgent(libs.test.mockito.core) {
                transitive = false
            }
            handler.testImplementation libs.test.mockito.kotlin
            handler.testImplementation libs.test.androidx.archtesting
        },

        addDependencyInjectionDependencies: { handler ->
            handler.implementation libs.hilt
            handler.kapt libs.hilt.compiler
        },

        addInstrumentationTestDependencies: { handler ->
            handler.androidTestImplementation libs.test.junit
            handler.androidTestImplementation libs.test.junit.kotlin
            handler.androidTestImplementation libs.test.mockito.core
            handler.androidTestImplementation libs.test.mockito.kotlin

            handler.androidTestImplementation libs.test.androidx.junit // Junit runner
            handler.androidTestImplementation libs.test.androidx.archtesting
            handler.androidTestImplementation libs.test.androidx.testrunner
            handler.androidTestImplementation libs.test.androidx.core
            handler.androidTestImplementation libs.test.androidx.espresso.core
            handler.androidTestImplementation libs.test.androidx.espresso.intents

            handler.androidTestImplementation libs.hilt.android.testing
            handler.androidTestAnnotationProcessor libs.hilt.android.compiler
            handler.kaptAndroidTest libs.hilt.android.compiler
        },
]
